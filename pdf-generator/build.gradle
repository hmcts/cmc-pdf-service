apply plugin: 'java'

def flyingSaucerVersion = '9.1.16'

dependencyManagement {
  dependencies {
    // spring boot drags in 1.1.11 and `java-logging` already is on 1.2.3
    dependencySet(group: 'ch.qos.logback', version: '1.2.3') {
      entry 'logback-core'
      entry 'logback-classic'
    }
    // spring boot dependency management plugin downgrades
    // bump to 2.9.8 because of https://nvd.nist.gov/vuln/detail/CVE-2018-7489
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.9.8') {
      entry 'jackson-annotations'
      entry 'jackson-core'
      entry 'jackson-databind'
    }
  }
  imports {
    mavenBom "org.xhtmlrenderer:flying-saucer-pdf:${flyingSaucerVersion}"
  }
}

def versions = [
  openPdf         : dependencyManagement.importedProperties['openpdf.version'],
  flyingSaucer    : flyingSaucerVersion
]

dependencies {
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: project(':').versions.reformJavaLogging

  compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

  compile group: 'org.xhtmlrenderer', name: 'flying-saucer-core', version: versions.flyingSaucer
  compile group: 'org.xhtmlrenderer', name: 'flying-saucer-pdf', version: versions.flyingSaucer, {
    exclude group: 'com.lowagie'
    exclude group: 'org.bouncycastle'
  }
  compile group: 'com.github.librepdf', name: 'openpdf', version: versions.openPdf
}
